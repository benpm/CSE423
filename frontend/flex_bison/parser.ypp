%{
    #include <vector>
    #include <spdlog/spdlog.h>
    #include <spdlog/sinks/stdout_color_sinks.h>
    #include <spdlog/fmt/fmt.h>
    #include <parsetree.hpp>

    extern int yylex();
    extern int yyparse();

    void yyerror(const char *s);

    PT* parserYppHandle;
%}
%locations
%define parse.error verbose

// Bison fundamentally works by asking flex to get the next token, which it
// returns as an object of type "yystype".  Initially (by default), yystype
// is merely a typedef of "int", but for non-trivial projects, tokens could
// be of any arbitrary data type.  So, to deal with that, the idea is to
// override yystype's default typedef to be a C union instead.  Unions can
// hold all of the types of tokens that Flex could return, and this this means
// we can return ints or floats or strings cleanly.  Bison implements this
// mechanism with the %union directive:
%union {
    int ival;
    float fval;
    char *sval;
    char cval;
    PT* pt;
}

// Non-Terminal symbols
%type <pt> program
%type <pt> declarationList
%type <pt> declaration
%type <pt> varDeclaration
%type <pt> scopedVarDeclaration
%type <pt> varDeclList
%type <pt> varDeclInitialize
%type <pt> varDeclId
%type <pt> scopedTypeSpecifier
%type <pt> typeSpecifier
%type <pt> funDeclaration
%type <pt> funName
%type <pt> params
%type <pt> paramList
%type <pt> paramId
%type <pt> statement
%type <pt> expressionStmt
%type <pt> expression
%type <pt> mutable
%type <pt> assignop
%type <pt> unaryAssignop
%type <pt> simpleExpression
%type <pt> andExpression
%type <pt> unaryRelExpression
%type <pt> unaryAssignExpr
%type <pt> relExpression
%type <pt> relop
%type <pt> sumExpression
%type <pt> sumop
%type <pt> mulExpression
%type <pt> mulop
%type <pt> unaryExpression
%type <pt> factor
%type <pt> immutable
%type <pt> call
%type <pt> args
%type <pt> argList
%type <pt> constant
%type <pt> compoundStmt
%type <pt> localDeclarations
%type <pt> statementList
%type <pt> selectionStmt
%type <pt> elseIfList
%type <pt> iterationStmt
%type <pt> whileStmt
%type <pt> forStmt
%type <pt> returnStmt
%type <pt> breakStmt
%type <pt> labelStmt
%type <pt> gotoStmt
%type <pt> elseStmt
%type <pt> unaryMinus

//Terminal symbols
%token <sval> INT
%token <sval> FLOAT
%token <sval> CHAR

%token <sval> FOR
%token <sval> WHILE
%token <sval> IF
%token <sval> ELSE
%token <sval> RETURN
%token <sval> BREAK
%token <sval> GOTO

%token <cval> SEMICOLON
%token <cval> COLON
%token <cval> COMMA
%token <cval> LPAREN
%token <cval> RPAREN
%token <cval> LBRACE
%token <cval> RBRACE
%token <cval> LBRACK
%token <cval> RBRACK
%token <cval> EQUAL
%token <cval> PLUS
%token <cval> MINUS
%token <cval> TIMES
%token <cval> DIVIDE
%token <cval> MODULO
%token <sval> PLUSEQUAL
%token <sval> MINUSEQUAL
%token <sval> TIMESEQUAL
%token <sval> DIVEQUAL
%token <sval> MODEQUAL
%token <sval> INCR
%token <sval> DECR
%token <cval> LT
%token <cval> GT
%token <sval> LE
%token <sval> GE
%token <sval> ISEQ
%token <sval> NOTEQ
%token <sval> LOGAND
%token <sval> LOGOR
%token <cval> NOT

%token <sval> STRINGLIT
%token <cval> CHARLIT
%token <ival> INTCONST
%token <fval> FLOATCONST
%token <sval> ID

%token <sval> UNKNOWN

// Grammar productions
%%
program:
    declarationList
        {
            parserYppHandle->label = PT::PROGRAM;
            parserYppHandle->children = std::vector<PT*> { $1 };
            parserYppHandle->lineNum = yylloc.first_line;
        }
;
declarationList:
    declarationList declaration
        {
            $$ = new PT(PT::DECLARATION_LIST,
                std::vector<PT*> { $1, $2 }, yylloc.first_line);
        }
    | declaration
        {
            $$ = new PT(PT::DECLARATION_LIST,
                std::vector<PT*> { $1 }, yylloc.first_line);
        }
;
declaration:
    varDeclaration
        {
            $$ = new PT(PT::DECLARATION,
                std::vector<PT*> { $1 }, yylloc.first_line);
        }
    | funDeclaration
        {
            $$ = new PT(PT::DECLARATION,
                std::vector<PT*> { $1 }, yylloc.first_line);
        }
;
varDeclaration:
    typeSpecifier varDeclList SEMICOLON
        {
            $$ = new PT(PT::VAR_DECLARATION,
                std::vector<PT*> { $1, $2 }, yylloc.first_line);
        }
;
scopedVarDeclaration:
    scopedTypeSpecifier varDeclList SEMICOLON
        {
            $$ = new PT(PT::SCOPED_VAR_DECLARATION,
                std::vector<PT*> { $1, $2 }, yylloc.first_line);
        }
;
varDeclList:
    varDeclList COMMA varDeclInitialize
        {
            $$ = new PT(PT::VAR_DECL_LIST,
                std::vector<PT*> { $1, $3 }, yylloc.first_line);
        }
    | varDeclInitialize
        {
            $$ = new PT(PT::VAR_DECL_LIST,
                std::vector<PT*> { $1 }, yylloc.first_line);
        }
;
varDeclInitialize:
    varDeclId
        {
            $$ = new PT(PT::VAR_DECL_INITIALIZE,
                std::vector<PT*> { $1 }, yylloc.first_line);
        }
    | varDeclId EQUAL simpleExpression
        {
            PT *nEQUAL = new PT(PT::EQUAL, yylloc.first_line);
            $$ = new PT(PT::VAR_DECL_INITIALIZE,
                std::vector<PT*> { $1, nEQUAL, $3}, yylloc.first_line);
        }
;
varDeclId:
    ID
        {
            $$ = new PT(PT::ID, yylloc.first_line);
            $$->data.sval = $1;
        }
    | ID LBRACK RBRACK
        {
            $$ = new PT(PT::ARRAY_ID, yylloc.first_line);
        }
    | ID LBRACK constant RBRACK
        {
            $$ = new PT(PT::VAR_DECL_ID,
                std::vector<PT*> { $3 }, yylloc.first_line);
        }
;
scopedTypeSpecifier:
    typeSpecifier
        {
            $$ = new PT(PT::SCOPED_TYPE_SPECIFIER,
                std::vector<PT*> { $1 }, yylloc.first_line);
        }
;
typeSpecifier:
    INT
        {
            $$ = new PT(PT::INT, yylloc.first_line);
        }
    | FLOAT
        {
            $$ = new PT(PT::FLOAT, yylloc.first_line);
        }
    | CHAR
        {
            $$ = new PT(PT::CHAR, yylloc.first_line);
        }
;
funName:
    ID
        {
            $$ = new PT(PT::ID, yylloc.first_line);
            $$->data.sval = $1;
        }
;
funDeclaration:
    typeSpecifier funName LPAREN params RPAREN statement
        {
            $$ = new PT(PT::FUN_DECLARATION,
                std::vector<PT*> { $1, $2, $4, $6}, yylloc.first_line);
        }
;
params:
    paramList
        {
            $$ = new PT(PT::PARAMS,
                std::vector<PT*> { $1 }, yylloc.first_line);
        }
    | %empty
        {
            $$ = new PT(PT::PARAMS, yylloc.first_line);
        }
;
paramList:
    paramList COMMA typeSpecifier paramId
        {
            $$ = new PT(PT::PARAM_LIST,
                std::vector<PT*> { $1, $3, $4 }, yylloc.first_line);
        }
    | typeSpecifier paramId
        {
            $$ = new PT(PT::PARAM_LIST,
                std::vector<PT*> { $1, $2 }, yylloc.first_line);
        }
;
paramId:
    ID
        {
            $$ = new PT(PT::ID, yylloc.first_line);
            $$->data.sval = $1;
        }
    | ID LBRACK RBRACK
        {
            $$ = new PT(PT::ID, yylloc.first_line);
            $$->data.sval = $1;
        }
;
statement:
    expressionStmt
        {
            $$ = new PT(PT::STATEMENT,
                std::vector<PT*> { $1 }, yylloc.first_line);
        }
    | compoundStmt
        {
            $$ = new PT(PT::STATEMENT,
                std::vector<PT*> { $1 }, yylloc.first_line);
        }
    | selectionStmt
        {
            $$ = new PT(PT::STATEMENT,
                std::vector<PT*> { $1 }, yylloc.first_line);
        }
    | iterationStmt
        {
            $$ = new PT(PT::STATEMENT,
                std::vector<PT*> { $1 }, yylloc.first_line);
        }
    | returnStmt
        {
            $$ = new PT(PT::STATEMENT,
                std::vector<PT*> { $1 }, yylloc.first_line);
        }
    | breakStmt
        {
            $$ = new PT(PT::STATEMENT,
                std::vector<PT*> { $1 }, yylloc.first_line);
        }
    | labelStmt
        {
            $$ = new PT(PT::STATEMENT,
                std::vector<PT*> { $1 }, yylloc.first_line);
        }
    | gotoStmt
        {
            $$ = new PT(PT::STATEMENT,
                std::vector<PT*> { $1 }, yylloc.first_line);
        }
;
expressionStmt:
    expression SEMICOLON
        {
            $$ = new PT(PT::EXPRESSION_STMT,
                std::vector<PT*> { $1 }, yylloc.first_line);
        }
    | SEMICOLON
        {
            $$ = new PT();
        }
;
expression:
    mutable assignop expression
        {
            $$ = new PT(PT::EXPRESSION,
                std::vector<PT*> { $1, $2, $3 }, yylloc.first_line);
        }
    | unaryAssignExpr
        {
            $$ = new PT(PT::EXPRESSION,
                std::vector<PT*> { $1 }, yylloc.first_line);
        }
    | simpleExpression
        {
            $$ = new PT(PT::EXPRESSION,
                std::vector<PT*> { $1 }, yylloc.first_line);
        }
;
unaryAssignExpr:
    unaryAssignop mutable
        {
            $$ = new PT(PT::UNARY_ASSIGN_EXPRESSION,
                std::vector<PT*> { $1, $2 }, yylloc.first_line);
        }
    | mutable unaryAssignop
        {
            $$ = new PT(PT::UNARY_ASSIGN_EXPRESSION,
                std::vector<PT*> { $1, $2 }, yylloc.first_line);
        }
;
mutable:
    ID
        {
            $$ = new PT(PT::ID, yylloc.first_line);
            $$->data.sval = $1;
        }
    | mutable LBRACK expression RBRACK
        {
            $$ = new PT(PT::MUTABLE,
                std::vector<PT*> { $1, $3 }, yylloc.first_line);
        }
;
assignop:
    EQUAL
        {
            $$ = new PT(PT::EQUAL, yylloc.first_line);
        }
    | PLUSEQUAL
        {
            $$ = new PT(PT::PLUSEQUAL, yylloc.first_line);
        }
    | MINUSEQUAL
        {
            $$ = new PT(PT::MINUSEQUAL, yylloc.first_line);
        }
    | TIMESEQUAL
        {
            $$ = new PT(PT::TIMESEQUAL, yylloc.first_line);
        }
    | DIVEQUAL
        {
            $$ = new PT(PT::DIVEQUAL, yylloc.first_line);
        }
    | MODEQUAL
        {
            $$ = new PT(PT::MODEQUAL, yylloc.first_line);
        }
;
unaryAssignop:
    INCR
        {
            $$ = new PT(PT::INCR, yylloc.first_line);
        }
    | DECR
        {
            $$ = new PT(PT::DECR, yylloc.first_line);
        }
;
simpleExpression:
    simpleExpression LOGOR andExpression
        {
            PT* nLOGOR = new PT(PT::LOGOR, yylloc.first_line);
            $$ = new PT(PT::SIMPLE_EXPRESSION,
                std::vector<PT*> { $1, nLOGOR, $3 }, yylloc.first_line);
        }
    | andExpression
        {
            $$ = new PT(PT::SIMPLE_EXPRESSION,
                std::vector<PT*> { $1 }, yylloc.first_line);
        }
;
andExpression:
    andExpression LOGAND unaryRelExpression
        {
            PT* nLOGAND = new PT(PT::LOGAND, yylloc.first_line);
            $$ = new PT(PT::AND_EXPRESSION,
                std::vector<PT*> { $1, nLOGAND, $3 }, yylloc.first_line);
        }
    | unaryRelExpression
        {
            $$ = new PT(PT::AND_EXPRESSION,
                std::vector<PT*> { $1 }, yylloc.first_line);
        }
;
unaryRelExpression:
    NOT unaryRelExpression
        {
            PT* nNOT = new PT(PT::NOT, yylloc.first_line);
            $$ = new PT(PT::UNARY_REL_EXPRESSION,
                std::vector<PT*> { nNOT, $2 }, yylloc.first_line);
        }
    | relExpression
        {
            $$ = new PT(PT::UNARY_REL_EXPRESSION,
                std::vector<PT*> { $1 }, yylloc.first_line);
        }
;
relExpression:
    sumExpression relop sumExpression
        {
            $$ = new PT(PT::REL_EXPRESSION,
                std::vector<PT*> { $1, $2, $3 }, yylloc.first_line);
        }
    | sumExpression
        {
            $$ = new PT(PT::REL_EXPRESSION,
                std::vector<PT*> { $1 }, yylloc.first_line);
        }
;
relop:
    LE
        {
            $$ = new PT(PT::LE, yylloc.first_line);
        }
    | LT
        {
            $$ = new PT(PT::LT, yylloc.first_line);
        }
    | GT
        {
            $$ = new PT(PT::GT, yylloc.first_line);
        }
    | GE
        {
            $$ = new PT(PT::GE, yylloc.first_line);
        }
    | ISEQ
        {
            $$ = new PT(PT::ISEQ, yylloc.first_line);
        }
    | NOTEQ
        {
            $$ = new PT(PT::NOTEQ, yylloc.first_line);
        }
;
sumExpression:
    sumExpression sumop mulExpression
        {
            $$ = new PT(PT::SUM_EXPRESSION,
                std::vector<PT*> { $1, $2, $3 }, yylloc.first_line);
        }
    | mulExpression
        {
            $$ = new PT(PT::SUM_EXPRESSION,
                std::vector<PT*> { $1 }, yylloc.first_line);
        }
;
sumop:
    PLUS
        {
            $$ = new PT(PT::PLUS, yylloc.first_line);
        }
    | MINUS
        {
            $$ = new PT(PT::MINUS, yylloc.first_line);
        }
;
mulExpression:
    mulExpression mulop unaryExpression
        {
            $$ = new PT(PT::MUL_EXPRESSION,
                std::vector<PT*> { $1, $2, $3 }, yylloc.first_line);
        }
    | unaryExpression
        {
            $$ = new PT(PT::MUL_EXPRESSION,
                std::vector<PT*> { $1 }, yylloc.first_line);
        }
;
mulop:
    TIMES
        {
            $$ = new PT(PT::TIMES, yylloc.first_line);
        }
    | DIVIDE
        {
            $$ = new PT(PT::DIVIDE, yylloc.first_line);
        }
    | MODULO
        {
            $$ = new PT(PT::MODULO, yylloc.first_line);
        }
;
unaryExpression:
    unaryMinus
        {
            $$ = new PT(PT::UNARY_EXPRESSION,
                std::vector<PT*> { $1 }, yylloc.first_line);
        }
    | factor
        {
            $$ = new PT(PT::UNARY_EXPRESSION,
                std::vector<PT*> { $1 }, yylloc.first_line);
        }
;
unaryMinus:
    MINUS unaryExpression
        {
            $$ = new PT(PT::UNARY_MINUS,
                std::vector<PT*> { $2 }, yylloc.first_line);
        }
;
factor:
    immutable
        {
            $$ = new PT(PT::FACTOR,
                std::vector<PT*> { $1 }, yylloc.first_line);
        }
    | mutable
        {
            $$ = new PT(PT::FACTOR,
                std::vector<PT*> { $1 }, yylloc.first_line);
        }
;
immutable:
    LPAREN expression RPAREN
        {
            $$ = new PT(PT::IMMUTABLE,
                std::vector<PT*> { $2 }, yylloc.first_line);
        }
    | call
        {
            $$ = new PT(PT::IMMUTABLE,
                std::vector<PT*> { $1 }, yylloc.first_line);
        }
    | constant
        {
            $$ = new PT(PT::IMMUTABLE,
                std::vector<PT*> { $1 }, yylloc.first_line);
        }
;
call:
    ID LPAREN args RPAREN
        {
            PT* tID = new PT(PT::ID, yylloc.first_line);
            tID->data.sval = $1;
            $$ = new PT(PT::CALL,
                std::vector<PT*> { tID, $3 }, yylloc.first_line);
        }
;
args:
    argList
        {
            $$ = new PT(PT::ARGS,
                std::vector<PT*> { $1 }, yylloc.first_line);
        }
    | %empty
        {
            $$ = new PT();
        }
;
argList:
    argList COMMA expression
        {
            $$ = new PT(PT::ARG_LIST,
                std::vector<PT*> { $1, $3 }, yylloc.first_line);
        }
    | expression
        {
            $$ = new PT(PT::ARG_LIST,
                std::vector<PT*> { $1 }, yylloc.first_line);
        }
;
constant:
    STRINGLIT
        {
            $$ = new PT(PT::STRINGLIT, yylloc.first_line);
            $$->data.sval = $1;
        }
    | CHARLIT
        {
            $$ = new PT(PT::CHARLIT, yylloc.first_line);
            $$->data.cval = $1;
        }
    | FLOATCONST
        {
            $$ = new PT(PT::FLOATCONST, yylloc.first_line);
            $$->data.fval = $1;
        }
    | INTCONST
        {
            $$ = new PT(PT::INTCONST, yylloc.first_line);
            $$->data.ival = $1;
        }
;
compoundStmt:
    LBRACE localDeclarations statementList RBRACE
        {
            $$ = new PT(PT::COMPOUND_STMT,
                std::vector<PT*> { $2, $3 }, yylloc.first_line);
        }
;
localDeclarations:
    localDeclarations scopedVarDeclaration
        {
            $$ = new PT(PT::LOCAL_DECLARATIONS,
                std::vector<PT*> { $1, $2 }, yylloc.first_line);
        }
    | %empty
        {
            $$ = new PT(PT::LOCAL_DECLARATIONS, yylloc.first_line);
        }
;
statementList:
    statementList statement
        {
            $$ = new PT(PT::STATEMENT_LIST,
                std::vector<PT*> { $1, $2 }, yylloc.first_line);
        }
    | %empty
        {
            $$ = new PT();
        }
;
selectionStmt:
    IF LPAREN simpleExpression RPAREN compoundStmt elseIfList
        {
            PT* tIF = new PT(PT::IF, yylloc.first_line);
            $$ = new PT(PT::SELECTION_STMT,
                std::vector<PT*> { tIF, $3, $5, $6 }, yylloc.first_line);
        }
    | IF LPAREN simpleExpression RPAREN compoundStmt elseIfList elseStmt
        {
            PT* tIF = new PT(PT::IF, yylloc.first_line);
            $$ = new PT(PT::SELECTION_STMT,
                std::vector<PT*> { tIF, $3, $5, $6, $7 }, yylloc.first_line);
        }
;
elseStmt:
    ELSE compoundStmt
        {
            PT* tELSE = new PT(PT::ELSE, yylloc.first_line);
            $$ = new PT(PT::ELSE_STMT,
                std::vector<PT*> { tELSE, $2 }, yylloc.first_line);
        }
;
elseIfList:
    elseIfList ELSE IF LPAREN simpleExpression RPAREN compoundStmt
        {
            PT* tELSEIF = new PT(PT::ELSE_IF, yylloc.first_line);
            $$ = new PT(PT::ELSE_IF_LIST,
                std::vector<PT*> { $1, tELSEIF, $5, $7 }, yylloc.first_line);
        }
    | %empty
        {
            $$ = new PT();
        }
;
iterationStmt:
    whileStmt
        {
            $$ = new PT(PT::ITERATION_STMT,
                std::vector<PT*> { $1 }, yylloc.first_line);
        }
    | forStmt
        {
            $$ = new PT(PT::ITERATION_STMT,
                std::vector<PT*> { $1 }, yylloc.first_line);
        }
;
whileStmt:
    WHILE LPAREN simpleExpression RPAREN statement
        {
            $$ = new PT(PT::WHILE_STMT,
                std::vector<PT*> { $3, $5 }, yylloc.first_line);
        }
;
forStmt:
    FOR LPAREN varDeclaration relExpression SEMICOLON expression RPAREN statement
        {
            $$ = new PT(PT::FOR_STMT,
                std::vector<PT*> { $3, $4, $6, $8 }, yylloc.first_line);
        }
;
returnStmt:
    RETURN SEMICOLON
        {
            $$ = new PT(PT::RETURN_STMT, yylloc.first_line);
        }
    | RETURN expression SEMICOLON
        {
            $$ = new PT(PT::RETURN_STMT,
                std::vector<PT*> { $2 }, yylloc.first_line);
        }
;
breakStmt:
    BREAK SEMICOLON
        {
            PT* tBREAK = new PT(PT::BREAK, yylloc.first_line);
            $$ = new PT(PT::BREAK_STMT,
                std::vector<PT*> { tBREAK }, yylloc.first_line);
        }
;
labelStmt:
    ID COLON
        {
            PT* tID = new PT(PT::ID, yylloc.first_line);
            tID->data.sval = $1;
            $$ = new PT(PT::LABEL_STMT,
                std::vector<PT*> { tID }, yylloc.first_line);
        }
;
gotoStmt:
    GOTO ID SEMICOLON
        {
            PT* tGOTO = new PT(PT::GOTO, yylloc.first_line);
            PT* tID = new PT(PT::ID, yylloc.first_line);
            tID->data.sval = $2;
            $$ = new PT(PT::GOTO_STMT,
                std::vector<PT*> { tGOTO, tID }, yylloc.first_line);
        }
%%

/**
 * @brief Error function called if one is encountered during parsing
 *
 * @param s Error string
 */
void yyerror(const char *s)
{
	spdlog::error("{} on line {}", s, yylloc.first_line);
	exit(-1);
}